;;;
;;;	EMILY Board Console Driver (for 8051)
;;;

	INCLUDE	"../../emily.inc"

INIT:
	MOV	A,#00H
	MOV	DPTR,#SMBASE+EO_HSK
	MOVX	@DPTR,A
	MOV	DPTR,#SMBASE+EO_CMD ; Command
	MOVX	@DPTR,A

	MOV	A,#EG_SIG
	MOV	DPTR,#SMBASE+EO_SIG ; Signature
	MOVX	@DPTR,A
	
	MOV	A,#EH_REQ
	MOV	DPTR,#SMBASE+EO_HSK ; Handshake
	MOVX	@DPTR,A

	RET

CONIN:
	PUSH	DPH
	PUSH	DPL
	MOV	DPTR,#SMBASE+EO_HSK
CIN0:
	MOVX	A,@DPTR
	CJNE	A,#EH_ACK,CIN0

	MOV	DPTR,#SMBASE+EO_CMD
	MOV	A,#EC_CIN	; CONIN Command
	MOVX	@DPTR,A

	MOV	DPTR,#SMBASE+EO_HSK
	MOV	A,#EH_REQ
	MOVX	@DPTR,A

	MOV	DPTR,#SMBASE+EO_HSK
CIN1:
	MOVX	A,@DPTR
	CJNE	A,#EH_ACK,CIN1

	MOV	DPTR,#SMBASE+EO_DAT
	MOVX	A,@DPTR

	POP	DPL
	POP	DPH
	RET

CONST:
	PUSH	DPH
	PUSH	DPL
	MOV	DPTR,#SMBASE+EO_HSK
CST0:
	MOVX	A,@DPTR
	CJNE	A,#EH_ACK,CST0

	MOV	DPTR,#SMBASE+EO_CMD
	MOV	A,#EC_CST	; CONST Command
	MOVX	@DPTR,A

	MOV	DPTR,#SMBASE+EO_HSK
	MOV	A,#EH_REQ
	MOVX	@DPTR,A

	MOV	DPTR,#SMBASE+EO_HSK
CST1:
	MOVX	A,@DPTR
	CJNE	A,#EH_ACK,CST1

	MOV	DPTR,#SMBASE+EO_DAT
	MOVX	A,@DPTR

	POP	DPL
	POP	DPH
	RET	

CONOUT:
	PUSH	DPH
	PUSH	DPL
	PUSH	ACC
	MOV	DPTR,#SMBASE+EO_HSK
COUT0:
	MOVX	A,@DPTR
	CJNE	A,#EH_ACK,COUT0
	
	MOV	DPTR,#SMBASE+EO_CMD
	MOV	A,#EC_COT	; CONOUT Command
	MOVX	@DPTR,A

	MOV	DPTR,#SMBASE+EO_DAT	
	POP	ACC
	MOVX	@DPTR,A

	MOV	DPTR,#SMBASE+EO_HSK
	MOV	A,#EH_REQ
	MOVX	@DPTR,A

	POP	DPL
	POP	DPH
	RET
