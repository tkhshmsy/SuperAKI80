;;;
;;;	Zilog Z8530 SCC Console Driver
;;;

INIT:
	LD	R1,#SCCAC
	LDA	AR3,INITAB
	LD	R0,#(INITABE-INITAB)
	OTIRB	@R1,@AR3,R0
	RET

INITAB:
	DB	00H		; Dummy
	DB	09H, 0C0H	; WR9: Force Hardware Reset
	DB	04H, WR4_V	; WR4
	DB	03H, WR3_V	; WR3
	DB	05H, WR5_V	; WR5
	DB	0BH, WR11_V	; WR11
	DB	0CH, low(BRGTC)	; WR12
	DB	0DH,high(BRGTC)	; WR13
	DB	0EH, 03H	; WR14: PCLK, Enable
	DB	03H, WR3_V|01H	; WR3: Rx Enable
	DB	05H, WR5_V|08H	; WR5: Tx Enable
INITABE:
	ALIGN	2

CONIN:
	INB	RH0,SCCAC
	ANDB	RH0,#01H
	JR	Z,CONIN

	INB	RL0,SCCAD

	RET

CONST:
	INB	RL0,SCCAC
	ANDB	RL0,#01H

	RET

CONOUT:
	INB	RH0,SCCAC
	ANDB	RH0,#04H
	JR	Z,CONOUT

	OUTB	SCCAD,RL0

	RET

